# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=psmisc
_tag='3c53de31d8797fc20e1fbf2b32732f64db0b9f42' # git rev-parse v${_tag_name}
pkgver=23.7
pkgrel=1
pkgdesc='Miscellaneous procfs tools'
arch=('loong64' 'x86_64')
url='https://gitlab.com/psmisc/psmisc'
license=('GPL')
depends=('ncurses')
makedepends=('git' 'po4a')
validpgpkeys=('5D2FB320B825D93904D205193938F96BDF50FEA5') # Craig Small <csmall@debian.org>
source=("git+https://gitlab.com/psmisc/psmisc.git#tag=${_tag}?signed")
sha256sums=('SKIP')

prepare() {
  cd ${pkgname}

  ./autogen.sh
}

build() {
  cd ${pkgname}

  ./configure \
    --prefix=/usr
  make
}

package() {
  cd ${pkgname}

  make DESTDIR="${pkgdir}" install
}
