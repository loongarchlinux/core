# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=mkinitcpio
pkgver=39
pkgrel=1
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url='https://gitlab.archlinux.org/archlinux/mkinitcpio/mkinitcpio'
license=('GPL-2.0-only')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive' 'coreutils'
         'bash' 'binutils' 'diffutils' 'findutils' 'grep' 'filesystem>=2011.10-1' 'zstd' 'systemd')
makedepends=('asciidoc')
checkdepends=('bats' 'bats-assert' 'lzop')
makedepends=('asciidoc')
optdepends=('gzip: Use gzip compression for the initramfs image'
            'xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
provides=('initramfs')
backup=('etc/mkinitcpio.conf')
source=("https://sources.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig}
	"0001-mkinitcpio-loongarch64-lib64-links.patch"
	"mkinitcpio-fix-error.patch")
sha512sums=('50ae08ddf7596b821c89f927ff6692ca907a5045590096155ec6de64e0b5da647ff2c75d1603ab0035046096d37a6eacae383d458b1bb36bd525691b2b23c9ea'
            'SKIP'
            'f4a50632ad6758a100a10d4a78c8697e12a089946f94fbe28c83b3129f480a7c572c5b306e047a0a36f6dc095d3a1e908e47062090af610a5b961c9e3de093e3'
            '33e903a41168f156318a9b03809717a07d56911837bee8f2718a21b0e08b6f8ff361a13a26ff202006a00dfa90f00f2e0efa6cf83db1038833f7d600d3438772')
b2sums=('3a28d6711ce843aafe2868b09ba5a723e57e4fdffe905ac915aa5d224279590a10bba902933f183de33e08e96a6aad4d9e1d4752be74c011a37c4cbe83c8d645'
        'SKIP'
        '0de0d74ffd7842b3946979e19f4213a659f8cf6dcbfef1ffd02b37e97aaef63f56ef973dc2358ece2ced29549bd8049473536d70f002993da3c6d726402d11c2'
        'b9ccc1c97cadf88aec03d549d8bdf89536b95a64b05d316481815faca06c94aa867523c3cbfda352c44956c4c5959b42f70c05e71cb51278446f2389566ef62b')
validpgpkeys=('ECCAC84C1BA08A6CC8E63FBBF22FB1D78A77AEAB'    # Giancarlo Razzolini
              'C100346676634E80C940FB9E9C02FF419FECBE16')   # Morten Linderud

prepare() {
  patch -p1 -d "$pkgname-$pkgver" -i $srcdir/0001-mkinitcpio-loongarch64-lib64-links.patch
  patch -p1 -d "$pkgname-$pkgver" -i $srcdir/mkinitcpio-fix-error.patch
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
